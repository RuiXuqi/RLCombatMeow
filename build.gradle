buildscript {
    repositories {
        maven {
            url = 'https://maven.minecraftforge.net/'
        }
        maven {
            url = 'https://repo.spongepowered.org/maven'
        }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7.+'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

version = "${minecraft_version}-${mod_version}"
group = "bettercombat.mod"
archivesBaseName = "${mod_name}"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: mappings_channel, version: mappings_version
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url = "https://www.cursemaven.com"
    }
    maven {
        url = 'https://maven.cleanroommc.com'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    implementation fg.deobf("curse.maven:qualitytools-${qualitytools_version}")
    implementation fg.deobf("curse.maven:bettersurvival-${bettersurvival_version}")
    implementation fg.deobf("curse.maven:reachfix-${reachfix_version}")
    implementation fg.deobf("curse.maven:reskillable-${reskillable_version}")
    implementation fg.deobf("curse.maven:spartanweaponry-${spartanweaponry_version}")
    implementation fg.deobf("curse.maven:icenfirerlcraft-${icenfirerlcraft_version}")
    implementation fg.deobf("curse.maven:icenfire-${icenfire_version}")
    implementation fg.deobf("curse.maven:llibrary-${llibrary_version}")

    implementation ("zone.rong:mixinbooter:10.6") {
        transitive = false
    }
    annotationProcessor ("zone.rong:mixinbooter:10.6") {
        transitive = false
    }

    implementation "org.spongepowered:mixin:0.8.5"
    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
}

mixin {
    add sourceSets.main, "mixins.${mod_id}.refmap.json"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", minecraft_version

    filesMatching('mcmod.info') {
        expand 'mod_id': mod_id, 'mod_name': mod_name, 'version': project.version,
                'mcversion': minecraft_version, 'mod_description': mod_description,
                'mod_author': mod_author,
                'mod_url': mod_url
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : mod_name,
                "Specification-Version"   : "1",
                "Implementation-Title"    : mod_name,
                "Implementation-Version"  : mod_version,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "FMLCorePlugin": "${mixin_loader_class}",
                "FMLCorePluginContainsFMLMod": "true",
                "ForceLoadAsMod": "true",
                "TweakClass": "org.spongepowered.asm.launch.MixinTweaker"
        ])
    }
}

jar.finalizedBy('reobfJar')