buildscript {
    repositories {
        maven {
            url = 'https://maven.minecraftforge.net/'
        }
        maven {
            url = 'https://maven.gofancy.wtf/releases'
        }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'wtf.gofancy.fancygradle', name: 'wtf.gofancy.fancygradle.gradle.plugin', version: '1.1.2-3'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'wtf.gofancy.fancygradle'

fancyGradle {
    patches {
        resources
        coremods
        codeChickenLib
        asm
        mergetool
    }
}

version = "${minecraft_version}-${mod_version}"
group = "bettercombat.mod"
archivesBaseName = "${mod_name}"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: mappings_channel, version: mappings_version

    runs {
        client {
            args "--username=InDev"

            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url = "https://www.cursemaven.com"
    }
    flatDir {
        dirs 'libs\\'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    implementation fg.deobf("curse.maven:reachfix-${reachfix_version}")//Not released yet
    implementation files("libs/ReachFix-1.12.2-1.0.7-rc4.jar")
    implementation fg.deobf("curse.maven:qualitytools-${qualitytools_version}")
    implementation fg.deobf("curse.maven:llibrary-${llibrary_version}")
    implementation fg.deobf("curse.maven:icenfire-${icenfire_version}")
    implementation fg.deobf("curse.maven:potioncore-${potioncore_version}")
    implementation fg.deobf("curse.maven:spartanweaponry-${spartanweaponry_version}")
    implementation files("libs/SpartanWeaponry-1.12.2-1.5.0-indev-1.jar")
    implementation fg.deobf("curse.maven:reskillable-${reskillable_version}")
    implementation fg.deobf("curse.maven:bettersurvival-${bettersurvival_version}")
    implementation files("libs/better_survival-1.4.2-BETA6.jar")
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", minecraft_version

    filesMatching('mcmod.info') {
        expand 'mod_id': mod_id, 'mod_name': mod_name, 'version': project.version,
                'mcversion': minecraft_version, 'mod_description': mod_description,
                'mod_author': mod_author
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : mod_name,
                "Specification-Version"   : "1",
                "Implementation-Title"    : mod_name,
                "Implementation-Version"  : mod_version,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')